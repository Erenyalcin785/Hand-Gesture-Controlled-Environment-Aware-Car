#include <Wire.h>
#include <LiquidCrystal_I2C.h>
#include <SoftwareSerial.h>

LiquidCrystal_I2C lcd(0x27, 16, 2); 
SoftwareSerial bluetooth(11, 10);   // RX, TX

String inputData = "";

int d1 = 0, d2 = 0, d3 = 0, d4 = 0;
float temp = 0.0, hum = 0.0;
unsigned long lastSwitch = 0;
bool showDistances = true;

void setup() {
  Serial.begin(9600);
  bluetooth.begin(9600);
  lcd.begin();
  lcd.backlight();
  lcd.setCursor(0, 0);
  lcd.print("Veri bekleniyor");
}

void loop() {
  while (bluetooth.available()) {
    char c = bluetooth.read();
    if (c == '\n') {
      parseData(inputData);
      inputData = "";
    } else {
      inputData += c;
    }
  }

  if (millis() - lastSwitch > 2000) {
    lcd.clear();
    if (showDistances) {
      lcd.setCursor(0, 0);
      lcd.print("D1:");
      lcd.print(d1);
      lcd.print(" D2:");
      lcd.print(d2);
      lcd.setCursor(0, 1);
      lcd.print("D3:");
      lcd.print(d3);
      lcd.print(" D4:");
      lcd.print(d4);
    } else {
      lcd.setCursor(0, 0);
      lcd.print("Temp: ");
      lcd.print(temp);
      lcd.print("C");
      lcd.setCursor(0, 1);
      lcd.print("Hum : ");
      lcd.print(hum);
      lcd.print("%");
    }
    showDistances = !showDistances;
    lastSwitch = millis();
  }
}

void parseData(String data) {
  d1 = getValue(data, "D1:");
  d2 = getValue(data, "D2:");
  d3 = getValue(data, "D3:");
  d4 = getValue(data, "D4:");
  temp = getFloatValue(data, "T:");
  hum = getFloatValue(data, "H:");
}

int getValue(String data, String label) {
  int idx = data.indexOf(label);
  if (idx == -1) return 0;
  int endIdx = data.indexOf(',', idx);
  String val = data.substring(idx + label.length(), endIdx == -1 ? data.length() : endIdx);
  return val.toInt();
}

float getFloatValue(String data, String label) {
  int idx = data.indexOf(label);
  if (idx == -1) return 0;
  int endIdx = data.indexOf(',', idx);
  String val = data.substring(idx + label.length(), endIdx == -1 ? data.length() : endIdx);
  return val.toFloat();
}



















